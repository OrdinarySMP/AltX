plugins {
	id 'fabric-loom' version '1.10.1'
	id 'maven-publish'
	id 'com.github.johnrengelman.shadow' version "8+"
}

group = project.maven_group

base {
	archivesName = project.archives_base_name
}

configurations {
    embed
    compileOnly.extendsFrom(embed)
}

shadowJar {
    configurations = [project.configurations.embed]
    exclude('META-INF/services/**')
    relocate "com.moandjiezana", "dcshadow.com.moandjiezana"
}

repositories {
    mavenCentral()
    maven {
        url = uri("https://repo.opencollab.dev/main/")
    }
    maven { url 'https://jitpack.io' }

	// Add repositories to retrieve artifacts from in here.
	// You should only use this when depending on other mods because
	// Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
	// See https://docs.gradle.org/current/userguide/declaring_repositories.html
	// for more information about repositories.
	mavenCentral()
}

loom {
	splitEnvironmentSourceSets()

	mods {
		"altx" {
			sourceSet sourceSets.main
			sourceSet sourceSets.client
		}
	}
}

configurations {
	embed
	compileOnly.extendsFrom(embed)
}

shadowJar {
	configurations = [project.configurations.embed]
	relocate 'club.minnced', 'dcshadow.club.minnced'
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    include(modImplementation("me.lucko:fabric-permissions-api:${project.fabric_permission_api_version}"))

    embed implementation("club.minnced:discord-webhooks:${project.minnced_discord_webhook_version}")

    // config
    embed implementation("de.erdbeerbaerlp:toml4j:dd60f51")

    // integrations
    compileOnly "org.geysermc.floodgate:api:${project.floodgate_api_version}"
    compileOnly "com.github.OrdinarySMP:DiscordJustSync:${project.justsync_version}"

}

tasks.build.dependsOn(tasks.shadowJar)

processResources {
    def minecraft_version = project.minecraft_version
    if (project.last_minecraft_version != "") {
        minecraft_version = minecraft_version + " <=" + project.last_minecraft_version
    }

    inputs.property "version", project.version
	inputs.property "minecraft_version", minecraft_version
    inputs.property "loader_version", project.loader_version

	filesMatching("fabric.mod.json") {
        expand "version": project.version,
            "loader_version": project.loader_version,
            "minecraft_version": minecraft_version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

tasks.register("prodServer", net.fabricmc.loom.task.prod.ServerProductionRunTask) {
    def version = (project.hasProperty('version') && project.property('version') != 'unspecified') 
        ? project.property('version'): project.minecraft_version

    def fabricApiMetadataUrl = 'https://maven.fabricmc.net/net/fabricmc/fabric-api/fabric-api/maven-metadata.xml'

    def latestFabricApiVersion = null
    try {
        def xmlText = new URL(fabricApiMetadataUrl).text
        def parsedXml = new XmlSlurper().parseText(xmlText)
        def matchingVersions = parsedXml.versioning.versions.version.findAll {
            v -> v.text().endsWith("+${version}")
        }

        if (!matchingVersions.isEmpty()) {
            latestFabricApiVersion = matchingVersions[-1].text()
        }

    } catch (Exception e) {
        logger.warn("Failed to fetch or parse Fabric API metadata, falling back to default version: $e")
        latestFabricApiVersion = project.fabric_version 
        version = project.minecraft_version
    }

    if (latestFabricApiVersion == null) {
        throw new GradleException("No Fabric API version found for specified Minecraft version: ${version}")
    }

    configurations {
        register("runtimeMods")
    }

    dependencies.add("runtimeMods", "net.fabricmc.fabric-api:fabric-api:${latestFabricApiVersion}")
    mods.from(configurations.named("runtimeMods"))

    installerVersion = "1.0.1"
    loaderVersion = project.loader_version
    minecraftVersion = version
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

remapJar {
    // wait until the shadowJar is done
    dependsOn(shadowJar)
    mustRunAfter(shadowJar)
    // Set the input jar for the task. Here use the shadow Jar that include the .class of the transitive dependency
    inputFile = file(shadowJar.archiveFile)
}


jar {
	from("LICENSE") {
		rename { "${it}_${project.base.archivesName.get()}"}
	}
}

remapJar {
	// wait until the shadowJar is done
	dependsOn(shadowJar)
	mustRunAfter(shadowJar)
	// Set the input jar for the task. Here use the shadow Jar that include the .class of the transitive dependency
	inputFile = file(shadowJar.archiveFile)
}

artifacts {
    archives tasks.shadowJar
}


// configure the maven publication
publishing {
	publications {
		create("mavenJava", MavenPublication) {
			artifactId = project.archives_base_name
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}
